dnl
dnl

dnl Execute a maude command and compare it to the expected results.
dnl First parameter is the name of the test, second parameter is the maude
dnl script to execute for the text.  Number of rewrites will be filtered.
m4_define([TEST_MAUDE], [dnl
AT_CHECK([$MAUDE -no-banner -no-advise -batch $2 dnl
  | sed -f ${abs_srcdir}/maude.sed > $1.out])
AT_CHECK([diff -adNu $1.out ${abs_srcdir}/$1.out])
])

dnl Test our tool chain.  Requires maude,
dnl
dnl The test works as follows:
dnl Given an argument the test will first compile a maude version of the
dnl system.  This will be compared to some expected results.
dnl Then a set of tests, defined in [name]-tests.maude are run on this
dnl model and the output of maude is again compared to some expected
dnl results.
m4_define([TEST_CREOLC], [dnl
AT_SETUP([$1])
AT_CHECK([creolc ${abs_srcdir}/$1.creol -o $1.maude])
AT_CHECK([diff -adNu ${abs_srcdir}/$1.maude $1.maude])
AT_DATA([test.maude],
[
set show timing off .
set show advise off .
load $1 .
$2
quit .
])
TEST_MAUDE([$1], test.maude)
AT_DATA([metatest.maude],
[
set show timing off .
set show advise off .
load creol-interpreter .
load $1 .
load creol-metaexec .
$3
quit .
])
dnl TEST_MAUDE([meta-$1], metatest.maude) dnl Don't exec this, it takes ages.
AT_CLEANUP
])

m4_define([TEST_CREOLMC], [dnl
AT_SETUP([$1])
AT_CHECK([creolc -target maudemc ${abs_srcdir}/$1.creol -o $1.maude])
AT_CHECK([diff -adNu ${abs_srcdir}/$1.maude $1.maude])
AT_DATA([test.maude],
[
set show timing off .
set show advise off .
load $1 .
$2
quit .
])
TEST_MAUDE([$1], test.maude)
AT_DATA([metatest.maude],
[
set show timing off .
set show advise off .
load creol-interpreter .
load $1 .
load creol-metaexec .
$3
quit .
])
dnl TEST_MAUDE([meta-$1], metatest.maude) dnl Don't exec this, it takes ages.
AT_CLEANUP
])

m4_define([TEST_CREOLC_PP],[dnl
AT_CHECK([creolc -target creol -o $1-pp1.creol ${abs_srcdir}/$1.creol])
AT_CHECK([creolc -target creol -o $1-pp2.creol $1-pp1.creol])
AT_CHECK([diff $1-pp1.creol $1-pp2.creol])
])

dnl test the xml export.
m4_define([TEST_CREOLC_XML],[dnl
AT_CHECK([creolc -target xml -o $1-pass1.xml ${abs_srcdir}/$1.creol])
])

AT_INIT([creolc])
AT_TESTED([creolc creolc.opt])

AT_SETUP([Datatypes])
TEST_MAUDE([Datatypes], ${abs_srcdir}/Datatypes.maude)
AT_CLEANUP

AT_SETUP([Signature])
TEST_MAUDE([Signature], ${abs_srcdir}/Signature.maude)
AT_CLEANUP

AT_SETUP([Interpreter])
TEST_MAUDE([Interpreter], ${abs_srcdir}/Interpreter.maude)
AT_CLEANUP

AT_SETUP([MetaExec])
TEST_MAUDE([MetaExec], ${abs_srcdir}/MetaExec.maude)
AT_CLEANUP

AT_SETUP([ModelChecker])
TEST_MAUDE([ModelChecker], ${abs_srcdir}/ModelChecker.maude)
AT_CLEANUP

AT_SETUP([PrettyPrinter])
TEST_CREOLC_PP([Inheritance])
TEST_CREOLC_PP([Diamond])
TEST_CREOLC_PP([MutRec])
TEST_CREOLC_PP([MutRec2])
TEST_CREOLC_PP([Interleave])
TEST_CREOLC_PP([Alarmclock])
TEST_CREOLC_PP([AsyncLCR])
TEST_CREOLC_PP([BoundedBuffer])
TEST_CREOLC_PP([Fibonacci])
TEST_CREOLC_PP([Philosophers])
TEST_CREOLC_PP([SantaClaus])
TEST_CREOLC_PP([SingleCellBuffer])
TEST_CREOLC_PP([MC3Phil])
TEST_CREOLC_PP([MC4Phil])
TEST_CREOLC_PP([MC5Phil])
AT_CLEANUP

AT_SETUP([XML-output])
TEST_CREOLC_XML([Inheritance])
TEST_CREOLC_XML([Diamond])
TEST_CREOLC_XML([MutRec])
TEST_CREOLC_XML([MutRec2])
TEST_CREOLC_XML([Interleave])
TEST_CREOLC_XML([Alarmclock])
TEST_CREOLC_XML([AsyncLCR])
TEST_CREOLC_XML([BoundedBuffer])
TEST_CREOLC_XML([Fibonacci])
TEST_CREOLC_XML([Philosophers])
TEST_CREOLC_XML([SantaClaus])
TEST_CREOLC_XML([SingleCellBuffer])
TEST_CREOLC_XML([MC3Phil])
TEST_CREOLC_XML([MC4Phil])
TEST_CREOLC_XML([MC5Phil])
AT_CLEANUP

TEST_CREOLC([Inheritance], [dnl
red init .
rew init main("B", int(1)) .
],[dnl
red simulate('PROGRAM, upTerm(init main("B", int(1))), 0, 1) .
])

TEST_CREOLC([Diamond], [dnl
red init .
rew init main("D", emp) .
],[dnl
red simulate('PROGRAM, upTerm(init main("D", emp)), 0, 1) .
])

TEST_CREOLC([Deadlock], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Butler", emp)), 0, 1) .
])

TEST_CREOLC([MutRec], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Test", emp)), 0, 1) .
])

TEST_CREOLC([MutRec2], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Test", emp)), 0, 1) .
])

TEST_CREOLC([Interleave], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Test", emp)), 0, 1) .
])

dnl This set of tests is testing our existing programs.
dnl

TEST_CREOLC([Alarmclock], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Alarmclock", emp)), 0, 1) .
])

TEST_CREOLC([AsyncLCR], [dnl
red init .
rew init main("Init", emp) .
],[
red simulate('PROGRAM, upTerm(init main("Init", emp)), 0, 1) .
])

TEST_CREOLC([Sieve], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Generator", emp)), 0, 1) .
])

TEST_CREOLC([AsyncSieveBuggy], [dnl
red init .

*** Shows the bug.
***
*** This test needs about 70 seconds and about 410 MiB memory.
*** Therefore, it is disabled at the user installation.
***
*** The model checker will find the bug in a second.
***
*** search [[1, 62]] in PROGRAM : init main("Generator", emp) =>+
***  < O:Oid : "Sieve" | Att: A:Subst, "p" |-> int(9), Pr:
***                      L:Subst, SL:StmList, PrQ: W:MProc, Lcnt: N:Nat >
***  conf:Configuration .
],[dnl
red simulate('PROGRAM, upTerm(init main("Generator", emp)), 0, 1) .
])

TEST_CREOLC([Philosophers], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Butler", emp)), 0, 1) .
])

TEST_CREOLC([SingleCellBuffer], [dnl
red init .
],[dnl
])

TEST_CREOLC([BoundedBuffer], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Starter", emp)), 0, 1) .
])

TEST_CREOLC([Fibonacci], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Fibonacci", int(6))), 0, 1) .
])

TEST_CREOLC([SantaClaus], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Christmas", emp)), 0, 1) .
])

TEST_CREOLC([PeerToPeer], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Christmas", emp)), 0, 1) .
])

TEST_CREOLMC([MC3Phil], [dnl
red init .
],[dnl
red simulate('PROGRAM, upTerm(init main("Butler", emp)), 0, 1) .
])

