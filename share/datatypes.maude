***
*** Datatypes for Creol
***
*** Copyright (c) 2007
***
*** This program is free software; you can redistribute it and/or
*** modify it under the terms of the GNU General Public License as
*** published by the Free Software Foundation; either version 2 of the
*** License, or (at your option) any later version.
***
*** This program is distributed in the hope that it will be useful, but
*** WITHOUT ANY WARRANTY; without even the implied warranty of
*** MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
*** General Public License for more details.
***
*** You should have received a copy of the GNU General Public License
*** along with this program; if not, write to the Free Software
*** Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA
*** 02111-1307, USA.
***

fmod CREOL-DATA-SIG is

  protecting META-LEVEL .
  protecting STRING .
  protecting NAT .

  sorts   Aid Oid Data Appl Expr .
  subsort Oid < Data < Expr .
  subsort Data < Appl < Expr .
  subsort String < Aid < Expr .

  op null    :           -> Data [ctor] .

  *** Needed in the model checker for tail recursion.
  op tag : Label -> Label [ctor format(c o)] .
  var L : Label .
  eq tag(tag(L)) = L .

  sorts   NeExprList ExprList NeDataList DataList .
  subsort Expr < NeExprList < ExprList .
  subsort Data < NeDataList < DataList .
  subsort DataList < ExprList .
  subsort NeDataList < NeExprList .

  op emp : -> DataList [ctor] .

  op _#_ : ExprList ExprList -> ExprList
	[ctor assoc id: emp prec 27 format (d r os d)] .
  op _#_ : NeExprList ExprList -> NeExprList [ctor ditto] .
  op _#_ : ExprList NeExprList -> NeExprList [ctor ditto] .
  op _#_ : DataList DataList  -> DataList [ctor ditto] .
  op _#_ : NeDataList DataList  -> NeDataList [ctor ditto] .
  op _#_ : DataList NeDataList  -> NeDataList [ctor ditto] .

  sorts NeAidList AidList .
  subsort Aid < NeAidList < AidList .

  op noAid : -> AidList [ctor] .

  op _,_ : AidList AidList -> AidList
	[ctor assoc id: noAid format (d r os d)] .
  op _,_ : NeAidList AidList -> NeAidList [ctor ditto] .
  op _,_ : AidList NeAidList -> NeAidList [ctor ditto] .

  op _(_) : String DataList -> Data [format (! o d d d) prec 12] .
  op _(_) : String ExprList -> Appl [ctor ditto] .

  op ob(_) : String -> Oid [ctor] .

  *** The interpreter should extend this module with his own definition
  *** of label values.  For example, the model checker uses his own version
  *** of labels.  This definition is the one used in the interpreter.
  *** op label : Nat -> Label [ctor] .
  sort Label .
  subsort Label < Data .

  op caller : Label -> Oid .

  var B : Bool .
  op bool(_) : Bool      -> Data [ctor format(o o ! o o)] .

  op _asBool            : Data -> Bool .
  eq bool(B) asBool     = B .

  vars D D'     : Data .
  vars E E'     : Expr .
  
  eq "equal" (D # D') = bool(D == D') .
  eq "not"(bool(false)) = bool(true) .  
  eq "not"(bool(true)) = bool(false) . 
  eq "or"(bool(true) # E) = bool(true) .
  eq "or"(bool(false) # E) = E .
  eq "and"(bool(false) # E) = bool(false) .
  eq "and"(bool(true) #  E) = E .  
endfm

view Aid from TRIV to CREOL-DATA-SIG is
  sort Elt to Aid .
endv

view Data from TRIV to CREOL-DATA-SIG is
  sort Elt to Data .
endv

view Expr from TRIV to CREOL-DATA-SIG is
  sort Elt to Expr .
endv

fmod CREOL-DATA-INT is
  extending CREOL-DATA-SIG .

  protecting INT .

  op int(_)  : Int       -> Data [ctor format(o o ! o o)] . 

  vars I I' : Int .
  var N : Nat .

  op _asInt : Data -> Int .
  eq int(I) asInt = I .

  op _asNat : Data -> Nat .
  eq int(N)  asNat      = N .

  eq "neg" (int(I)) = int(-(I)) .
  eq "less"  (int(I)# int(I')) = bool(I < I') .
  eq "lessEq" (int(I)# int(I')) = bool(I <= I') .
  eq "plus" (int(I)# int(I')) = int(I + I') .
  eq "minus" (int(I)# int(I')) = int( _-_(I, I')) .
  eq "times" (int(I)# int(I')) = int(I * I') .
  eq "div" (int(I)# int(I')) = int(I quo I') .
endfm

fmod CREOL-DATA-FLOAT is
  extending CREOL-DATA-SIG .

  protecting FLOAT .

  op float(_): Float     -> Data [ctor format(o o ! o o)] . 

  vars F F' : Float .

  op _asFloat : Data -> Float .
  eq float(F) asFloat = F .

  eq "less" (float(F)# float(F')) = bool(F < F') .
  eq "lessEq" (float(F)# float(F')) = bool(F <= F') .
  eq "plus" (float(F)# float(F')) = float(F + F') .
  eq "minus" (float(F)# float(F')) = float(_-_(F, F')) .
  eq "times" (float(F)# float(F')) = float(F * F') .
  eq "div" (float(F)# float(F')) = float(F / F') .
endfm

fmod CREOL-DATA-STRING is
  extending CREOL-DATA-SIG .

  protecting STRING .

  op str(_) : String    -> Data [ctor format(o o ! o o)] .

  vars S S' : String .

  op _asString : Data -> String .
  eq str(S) asString = S .

  eq "less" (str(S)# str(S')) = bool(S < S') .
  eq "lessEq" (str(S)# str(S')) = bool(S <= S') .
  eq "plus" (str(S)# str(S')) = str(S + S') .
endfm

fmod CREOL-DATA-LIST is
  extending CREOL-DATA-INT .

  op list : ExprList     -> Expr . *** lists
  op list : DataList     -> Data [ctor] . *** lists of data

  var N : Nat .
  vars D D'     : Data .
  vars E E'     : Expr .
  vars L L' L'' : ExprList .

  *** list-functions
  eq "head" (list(emp))	= null .
  eq "head" (list(E # L)) = E .
  eq "last" (list(emp))	= null .
  eq "last" (list(L # E)) = E .
  eq "rest" (list(emp))	= null .
  eq "rest" (list(L # E)) = list(L) .
  eq "tail" (list(emp))	= null .
  eq "tail" (list(E # L)) = list(L) .

  eq "length" (list(emp)) = int(0) .
  eq "length" (list(E # L)) = "plus" (int(1) # ("length" (list(L)))) .
  
  eq "isempty" (list(emp))  = bool(true) .
  eq "isempty" (list(E # L))= bool(false) .
  
  eq "remove" (list(E) #   E')  = if  E == E' then list(emp) else list(E) fi .
  eq "remove" (list(emp)# E )  = list(emp) .
  eq "remove" (list(E # L)# E')  = if E == E' then "remove" ((list(L))# E') 
			 else "plus" (list(E)# ("remove" ((list(L))# E'))) fi .

  eq "appendLeft" (list(L)# E) = list(E # L) .
  eq "appendRight" (list(L)# E) = list(L # E) .

  eq "has" (list(emp)# E ) = bool(false) .
  eq "has" (list(E # L) # E) = bool(true) .
  eq "has" (list(E # L) # E') = "has" (list(L)) [otherwise] .

  ***index starts at 1
  eq "after" (list(emp)# int(N))    = null . 
  eq "after" (list(E # L)# int(0))  = list(E # L) .
  eq "after" (list(E # L)# int(N))  =
    "after" ((list(L)) # "minus" (int(N) # int(1))) .

  eq "index" ((list(L))#   int(0))  = null .
  eq "index" ((list(emp))# int(N))  = null .
  eq "index" ((list(E # L))# int(N))= if (N == 1) then E else
				  "index" ((list(L))# int( _-_(N,1))) fi .

  eq "begwith" (list(E)# E')        = bool("head" (list(E)) == E') .
  eq "begwith" (list(E # L)# E')    = bool("head" (list(E # L)) == E') .
  eq "begwith" (list(E)# E')        = bool("last" (list(E)) == E') .
  eq "begwith" (list(E # L)# E')    = bool("last" (list(L)) == E') .

  eq "plus" (list(L)# list(L'))   = list(L # L') .
endfm

fmod CREOL-DATA-SET is
  extending CREOL-DATA-LIST .

  op setl : ExprList     -> Expr .        *** set
  op setl : DataList     -> Data [ctor] . *** set

  vars N        : Nat .
  vars D D'     : Data .
  vars E E'     : Expr .
  vars L L' L'' : ExprList .
  
  *** reduction for sets 
  eq  setl(L # E # L' # E # L'') = setl(L # E # L' # L'') .
  ceq setl(D # D' # L) = setl(D' # D # L) if ("less" (D' # D)) asBool .
  *** ceq setl(L # D # L' # D' # L'') = setl(L # D' # L' # D # L'') 
  *** if (D > D')asBool . *** old

  eq "length" (setl(E # L)) = int(1) + ("length" (setl(L))) .
  eq "length" (setl(E)#   E')  = if  E == E' then setl(emp) else setl(E) fi .
  eq "remove" (setl(emp)# E )  = setl(emp) .
  eq "remove" (setl(E # L)# E') = if E == E' then setl(L)
			 else "plus" (setl(E)#("remove" ((setl(L))# E'))) fi .

  eq "add" (setl(L) # E) =
    if "has" (setl(L) # E) asBool then setl(L) else setl(L # E) fi .    

  eq "has" (setl(emp) #  E ) = bool(false) .
  eq "has" (setl(E # L) # E) = bool(true) .
  eq "has" (setl(E # L) # E') = "has" (setl(L) # E') [otherwise] .
  eq "plus" (setl(L) # setl(L'))   = setl(L # L') .
endfm

fmod CREOL-DATA-PAIR is
  extending CREOL-DATA-SIG .
  sort Pair .
  subsort Pair < Data .

  op pair : Data Data -> Pair [ctor] .

  vars D D'     : Data .

  eq "pair" (D # D') = pair(D, D') .
  eq "fst" (pair(D, D')) = D . 
  eq "snd" (pair(D, D')) = D' .
endfm

fmod DATA is 

  extending CREOL-DATA-SIG .
  extending CREOL-DATA-INT .
  extending CREOL-DATA-FLOAT .
  extending CREOL-DATA-STRING .

endfm  

fmod DATATYPES is
  *** may easily keep various data types on separate file(s), 
  *** and plug in upon need.
  protecting DATA .
  extending CREOL-DATA-LIST .
  extending CREOL-DATA-SET .
  extending CREOL-DATA-PAIR .


  var L : ExprList .
 
  eq  "setl" (list(L)) = setl(L) .
  eq  "listl" (setl(L)) = list(L) .

endfm
